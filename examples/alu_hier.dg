#!/usr/bin/env -S python3 -m dg
import "/nmigen_dg/*"
import "/nmigen/cli/main"

Adder = subclass Elaboratable where
    __init__ = width ~> None where
        @a = Signal width
        @b = Signal width
        @o = Signal width

    elaborate = platform ~> m where with m = Module! =>

        Comb$ @o :== @a + @b



Subtractor = subclass Elaboratable where
    __init__ = width ~> None where
        @a = Signal width
        @b = Signal width
        @o = Signal width

    elaborate = platform ~> m where with m = Module! =>

        Comb$ @o :== @a - @b



ALU = subclass Elaboratable where
    __init__ = width ~> None where
        @op = Signal !
        @a  = Signal width
        @b  = Signal width
        @o  = Signal width

        @add = Adder      width
        @sub = Subtractor width

    elaborate = platform ~> m where with m = Module! =>

        Submodule.add$ @add
        Submodule.sub$ @sub

        Comb
            @add.a :== @a
            @sub.a :== @a
            @add.b :== @b
            @sub.b :== @b

        When
            @op, ->
                Comb$ @o :== @sub.o

            otherwise,->
                Comb$ @o :== @add.o




if __name__ == "__main__" =>
    alu = ALU width: 16
    main alu ports: [alu.op, alu.a, alu.b, alu.o]
