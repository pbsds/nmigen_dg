#!/usr/bin/env -S python3 -m dg
import "/nmigen_dg/*"
import "/nmigen/cli/main"


UARTReceiver = subclass Elaboratable where
    __init__ = divisor ~> None where
        @divisor = divisor

        @i    = Signal!
        @data = Signal 8
        @rdy  = Signal!
        @ack  = Signal!
        @err  = Signal!

    elaborate = platform ~> m where with m = Module! =>


        ctr = Signal$ range @divisor
        stb = Signal!
        when
            ctr == 0 ,->
                sync$ drive ctr (@divisor - 1)
                comb$ drive stb 1
            otherwise ,->
                sync$ drive ctr (ctr - 1)
        bit = Signal 3
        fsm = FSM
            START ->
                when ~ @i $ ->
                    START |>. DATA
                    sync
                        drive ctr (@divisor // 2)
                        drive bit 7

            DATA ->
                when stb $ ->
                    sync
                        drive bit   (bit - 1)
                        drive @data (Cat @i @data)

                    when (bit == 0) $ ->
                        DATA |>. STOP
            STOP ->
                when stb $ ->
                    when
                        @i ,->
                            STOP |>. DONE
                        otherwise ,->
                            STOP |>. ERROR
            DONE ->
                comb$ drive @rdy 1
                when @ack $ ->
                    DONE |>. START

            ERROR ->
                None
        comb$ drive @err $ fsm.ongoing "ERROR"




if __name__ == "__main__" =>
    rx = UARTReceiver 20
    main rx ports: [rx.i, rx.data, rx.rdy, rx.ack, rx.err]
