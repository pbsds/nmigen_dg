#!/usr/bin/env -S python3 -m dg
import "/nmigen_dg/*"
import "/nmigen/cli/main"


UARTReceiver = subclass Elaboratable where
    __init__ = divisor ~> None where
        @divisor = divisor

        @i    = Signal!
        @data = Signal 8
        @rdy  = Signal!
        @ack  = Signal!
        @err  = Signal!

    elaborate = platform ~> m where with m = Module! =>


        ctr = Signal$ range @divisor
        stb = Signal!
        when
            ctr == 0 ,->
                m.d.sync += ctr.eq(@divisor - 1)
                m.d.comb += stb.eq(1)
            otherwise ,->
                m.d.sync += ctr.eq(ctr - 1)
        bit = Signal 3
        fsm = FSM
            START ->
                when ~ @i ,-> # TODO: nani?
                    START.set_next "DATA"
                    sync
                        drive ctr @divisor // 2
                        drive bit 7

            DATA ->
                when stb ,->
                    sync
                        drive bit   (bit - 1)
                        drive @data (Cat @i  @data)

                    when bit == 0 ,->
                        DATA.set_next "STOP"
            STOP ->
                when stb ,->
                    when
                        @i ,->
                            STOP.set_next "DONE"
                        otherwise ,->
                            STOP.set_next "ERROR"
            DONE ->
                comb$ drive @rdy 1
                when @ack ,->
                    DONE.set_next "START"

            ERROR ->
                None
        comb$ drive @err $ fsm.ongoing "ERROR"




if __name__ == "__main__" =>
    rx = UARTReceiver 20
    main rx ports: [rx.i, rx.data, rx.rdy, rx.ack, rx.err]
